/*
 * Basic functionality for handling external (non-directly attached) subnets.
 *
 * The core of this module is a routing table with a list of subnets and their
 * corresponding gateway's IP address.
 *
 *
 * TODO:
 *  - should issue ARP requests for every nexthop (gateway) IP
 *    automatically, rather than requiring they be entered into the cached
 *    relation by hand at startup.
 */

/*******************************************************************************
 *
 * Data Structures
 *
 ******************************************************************************/

// Routing table for non-directly attached subnets
// subnet, mask, next-hop IP
// TABLE routes(ipaddr, int, ipaddr);

// router id, prefix, mask, outport
REMOTE TABLE ospfRoutes(switchid, ipaddr, int, portid)
  FROM routes AT 127.0.0.1 9999
  TIMEOUT 15 seconds;

/*******************************************************************************
 *
 * L3 routing to non-directly attached subnets
 *
 * TODO(adf): would be great if NAT were not explicitly considered here
 *
 ******************************************************************************/

// packets destined to outside whose source does NOT need NATing
// (NAT is handled by L3router.flg, even for non-local traffic)

ON ip_packet(pkt):
  DO forward(new) WHERE

    // generic nexthop IP selection
    // routes(addr, mask, nexthop)
    // and pkt.nwDst IN addr/mask

    // templated nexthop IP selection
    @nexthop-fragment
    AND
    // not in a local subnet for this router
    NOT (@dst-local-subnet)

    and subnets(nexthop_subnet, nexthop_mask, ANY, new.dlSrc, pkt.locSw, new.locPt)
    and nexthop IN nexthop_subnet/nexthop_mask
    and cached(nexthop, new.dlDst) // MAC addr of nexthop IP

    // don't route packets which need NAT'ing
    and NOT @needs-nat-disj
    //and needs_nat(needs_nat_subnet, needs_nat_mask)
    //and NOT (pkt.nwSrc IN needs_nat_subnet/needs_nat_mask)
    ;

/*******************************************************************************
 *
 * Per-subnet translators: outbound path to nexthop MAC addr where destination
 * MAC address is already set.
 *
 * TN note: the _inbound_ path for TR switches is handled in L3router.flg
 *
 ******************************************************************************/

ON ip_packet(pkt):
  DO forward(new) WHERE

    // generic nexthop IP selection
    // routes(addr, mask, nexthop)
    // and pkt.nwDst IN addr/mask

    // templated nexthop IP selection
    @nexthop-fragment-for-tr

    // not in a local subnet for this router
    // (prevent overlap with L3router.flg)
    and NOT (@dst-local-subnet)

    and subnets(nexthop_subnet, nexthop_mask, ANY, gwmac, router, rport)
    and router_tr(router, pkt.locSw)
    and nexthop IN nexthop_subnet/nexthop_mask
    and cached(nexthop, pkt.dlDst)
    and pkt.dlSrc = gwmac
    and pkt.nwDst != nexthop // this case is handled by internal routing
    and router_portmap(rport, new.locPt, pkt.locPt); // router -> host
